# test_medi_env_workflow.chor
# A test suite for our 'medi' CLI tool, using the BDD `test` syntax.
feature: "Note and Task Management"

settings {
  stop_on_failure = true
}

vars:
  EVAR = "## My medi note from an environment variable"

actors: Terminal

# A scenario groups related tests into a single workflow.
scenario "User can create a note, add a task, and clean up" {
    # Test 1: Verify that a note can be created and capture its name.
    test NoteCreationSuccess "Create a new note" {
        given:
            wait >= 0s
        when:
            Terminal runs "echo '$EVAR' | medi new blog-idea-for-dsl-test"
        then:
            Terminal output_matches "Successfully created note: '([\w-]+)'" as noteName
    }

    # Test 2: Verify that a task can be added to the created note.
    test TaskCanBeAdded "Add a task to the new note" {
        given:
            Test has_succeeded NoteCreationSuccess
        when:
            Terminal runs "medi task add ${noteName} 'Write the first draft'"
        then:
            Terminal output_contains "Added new task with ID:"
    }

    # Test 3: Verify that the added task appears in the task list.
    test TaskAppearsInList "Confirm task appears in list" {
        given:
            Test has_succeeded TaskCanBeAdded
        when:
            Terminal runs "medi task list"
        then:
            Terminal output_contains "Write the first draft (for note ${noteName})"
    }

    # This block runs after all tests in the scenario are complete.
    after {
        # Use the captured 'noteName' variable to delete the created note.
        Terminal runs "medi delete ${noteName} --force"
    }
}
# Example test scenario using httpbin endpoints

feature "Web Conditions"
actors: Web, Terminal

scenario "Testing various web conditions" {
    test StatusCodeTest "Test different HTTP status codes" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/status/200"
        then:
            Web response_status_is 200
            Web response_time is_below 15s # httpbin can be really slow sometimes
    }

    test ErrorStatusTest "Test error status codes" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/status/404"
        then:
            Web response_status_is 404
    }

    test SuccessStatusTest "Test response status is success" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/status/201"
        then:
            Web response_status is_success
    }

    test ErrorStatusConditionTest "Test response status is error" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/status/500"
        then:
            Web response_status is_error
    }

    test StatusInListTest "Test response status is in list" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/status/202"
        then:
            Web response_status is_in [200, 201, 202, 204]
    }

    test BodyContainsTest "Test response body contains text" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/json"
        then:
            Web response_status_is 200
            Web response_body_contains "slideshow"
            Web json_response at "/slideshow/slides" is_an_array
            Web json_response at "/slideshow/title" is_a_string
            Web json_response at "/slideshow/slides" has_size 2
            Web json_response at "/slideshow" is_an_object
    }

    test BodyMatchesTest "Test response body regex matching" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/user-agent"
        then:
            Web response_status_is 200
            Web response_body_matches 'user-agent":\s*"[^"]+"'
    }

    test JsonPathTest "Test JSON path existence" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/json"
        then:
            Web response_status_is 200
            Web json_body has_path "/slideshow/title"
    }

    test JsonValueTest "Test JSON path value equality" {
        given:
            Test can_start
        when:
            Web http_get "https://httpbin.io/json"
        then:
            Web response_status_is 200
            Web json_path at "/slideshow/title" equals "Sample Slide Show"
    }
}

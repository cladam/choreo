// choreo - A DSL for defining interactive test scenarios.
// This grammar defines the DSL structure and syntax.
// Human-readable documentation will be available in chromeo/README.md

// Ignore whitespace and comments.
WHITESPACE                        = _{ " " | "\t" | NEWLINE }
COMMENT                           = _{ "#" ~ (!NEWLINE ~ ANY)* }

// The program is a series of statements.
grammar                           = { SOI ~ statement* ~ EOI }
statement                         = { feature_def | settings_def | env_def | var_def | actors_def | background_def | scenario_def }

// Setting up the test suite.
settings_def                      = { "settings" ~ "{" ~ setting+ ~ "}" }
setting                           = { identifier ~ WHITESPACE* ~ "=" ~ WHITESPACE* ~ (value | binary_op) }
background_def                    = { "background" ~ "{" ~ (action | condition)+ ~ "}" }
env_def                           = { "env:" ~ identifier ~ ("," ~ identifier)* }
var_def                           = { "var" ~ identifier ~ WHITESPACE* ~ "=" ~ WHITESPACE* ~ value }
actors_def                        = { "actors:" ~ identifier ~ ("," ~ identifier)* }
feature_def                       = { "feature" ~ string }
scenario_def                      = { "scenario" ~ string ~ "{" ~ test* ~ after_block? ~ "}" }
after_block                       = { "after" ~ "{" ~ action+ ~ "}" }

// Defining a test case.
test                              = { "test" ~ identifier ~ string ~ "{" ~ given_block ~ when_block ~ then_block ~ "}" }
given_block                       = { "given:" ~ (action | condition)+ }
when_block                        = { "when:"  ~ action+ }
then_block                        = { "then:"  ~ condition+ }

// --- Generic Rule Block (for non-test logic) ---
rule                              = { "rule" ~ string ~ "{" ~ when_block_rule ~ then_block_rule ~ "}" }
when_block_rule                   = { "when:" ~ condition+ }
then_block_rule                   = { "then:" ~ action+ }

// --- Conditions & Actions ---
condition = {
    wait_condition |
    state_condition |
    filesystem_condition |
    terminal_condition
}
action                            = { type_action | press_action | run_action | filesystem_action }

// Condition Types
wait_condition                    = { "wait" ~ WHITESPACE* ~ comparison_op ~ WHITESPACE* ~ wait_marker }
state_condition                   = { "Test" ~ "has_succeeded" ~ identifier }
// --- Filesystem Conditions ---
file_is_not_empty_condition       = { "file" ~ string ~ "is_not_empty" }
file_is_empty_condition           = { "file" ~ string ~ "is_empty" }
filesystem_condition_keyword      = @{ "file_exists" | "file_does_not_exist" | "dir_exists" | "dir_does_not_exist" | "file_contains" }
filesystem_condition              = { "FileSystem" ~ (
    (filesystem_condition_keyword ~ string ~ "with_content" ~ string) |
    (filesystem_condition_keyword ~ string) |
    file_is_not_empty_condition |
    file_is_empty_condition
) }
terminal_condition                = { "Terminal" ~ (
    output_contains_condition |
    output_matches_condition |
    last_command_succeeded_cond |
    last_command_failed_cond |
    last_command_exit_code_is_cond |
    stdout_is_empty_condition |
    stderr_is_empty_condition |
    stderr_contains_condition |
    output_starts_with_condition |
    output_ends_with_condition |
    output_equals_condition |
    output_is_valid_json_condition |
    json_output_has_path_condition |
    json_output_at_equals_condition |
    json_output_at_includes_condition |
    json_output_at_has_item_count_condition
) }

// Terminal-specific condition parts
output_contains_condition         = { "output_contains" ~ string }
output_matches_condition          = { "output_matches" ~ string ~ ("as" ~ identifier)? }
last_command_succeeded_cond       = { "last_command" ~ "succeeded" }
last_command_failed_cond          = { "last_command" ~ "failed" }
last_command_exit_code_is_cond    = { "last_command" ~ "exit_code_is" ~ number }
stdout_is_empty_condition         = { "stdout_is_empty" }
stderr_is_empty_condition         = { "stderr_is_empty" }
stderr_contains_condition         = { "stderr_contains" ~ string }
output_starts_with_condition      = { "output_starts_with" ~ string }
output_ends_with_condition        = { "output_ends_with" ~ string }
output_equals_condition           = { "output_equals" ~ string }
output_is_valid_json_condition    = { "output_is_valid_json" }
json_output_has_path_condition    = { "json_output" ~ "has_path" ~ string }
json_output_at_equals_condition   = { "json_output" ~ "at" ~ string ~ "equals" ~ value }
json_output_at_includes_condition = { "json_output" ~ "at" ~ string ~ "includes" ~ value }
json_output_at_has_item_count_condition = { "json_output" ~ "at" ~ string ~ "has_item_count" ~ number }


// Action Types
type_action                       = { identifier ~ "types" ~ string }
press_action                      = { identifier ~ "presses" ~ string }
run_action                        = { identifier ~ "runs" ~ string }
filesystem_action                 = { "FileSystem" ~ filesystem_action_keyword ~ string ~ ("with_content" ~ string)? }
filesystem_action_keyword         = @{ "file" | "create_dir" | "delete_file" | "delete_dir" | "create_file" }

// The basic building blocks of the choreo language.
identifier                        = @{ ( "_" | 'a'..'z' | 'A'..'Z' ) ~ ( "_" | 'a'..'z' | 'A'..'Z' | '0'..'9' )* }
string                            = ${ "\"" ~ inner_double ~ "\"" | "'" ~ inner_single ~ "'" }
inner_double                      = @{ ( "\\" ~ ANY | !"\"" ~ ANY )* }
inner_single                      = @{ ( "\\" ~ ANY | !"'" ~ ANY )* }
number                            = @{ ASCII_DIGIT+ }
float                             = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
wait_unit                         = @{ "s" | "ms" }
wait_marker                       = @{ float ~ wait_unit }
value                             = { string | number }
comparison_op                     = @{ ">=" | "<=" | "==" | ">" | "<" }
binary_op                         = { "true" | "false" }
